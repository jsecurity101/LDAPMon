//
// inlineexecute-assembly based detection. Query checks LDAP searches where the parent has a namedpipe event
//
LDAPMON_Parser
| where not(tolower(LDAPQuery) has_any ("objectclass=*", "objectclass=dmd"))
| join kind=inner
(
SysmonEventParsed
| where EventID == 1
| where User !contains "NT AUTHORITY" and  Image !contains "Microsoft"
) on ProcessId, Computer
| join kind=inner (
SysmonEventParsed
| where EventID == 17
) on $left.ProcessGuid == $right.ProcessGuid, Computer
| summarize count() by Computer, User, ParentImage, ParentProcessGuid, ProcessGuid, Image, CommandLine, tolower(LDAPQuery), AttributeList, PipeName1